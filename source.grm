!!!!!!!!!!!
"Name"     = 'Compilador PICO'  
"Author"   = 'Joaquim Perez;Pedro Serrão;Francisco Pereira'
"Version"  = '1.0'
"About"    = 'Compilador para a linguagem PICO'
"Start Symbol" = <pico-program>                             
!!!!!!!!!!!

!!!!!!!!!!!
! SÍMBOLOS TERMINAIS
          
_Space               = Whitespace
{IdHead}             = {Letter}
{IdTail}             = {IdHead} + {Digit}
_Id                  = {IdHead}{IdTail}*
_Digits              = [123456789]{digit}*
_Quote               = ["]            
!!!!!!!!!!!
        
!!!!!!!!!!!
! CHARACTER SETS
<id>                 ::= _Id                  
<letter>             ::= _Letter
<digits>             ::= _Digits
<space>              ::= _Space
<quote>              ::= _Quote                      
<empty>              ::= <>
!!!!!!!!!!!                      
   
!!!!!!!!!!!
! BASE
<literal>            ::= '(' | ')' | '+' | '-' | ';' | '||' | ':' | ':='               
<natural-constant>   ::= <digits>                
<layout>             ::= <space>                                          
<any-char-but-quote> ::= <letter> | <digits> | <literal> | <layout>                      
<string-tail>        ::= <any-char-but-quote> <string-tail> | <quote>                      
<string-constant>    ::= <quote> <string-tail>
!!!!!!!!!!!                      
            
!!!!!!!!!!!
! OPERAÇÕES BÁSICAS                                                                
<id-type-list>       ::= <id> ':' <type> <empty> | <id> ':' <type> ',' <id-type-list>
<decls>              ::= 'declare' <id-type-list> ';'                                          
<type>               ::= 'natural' | 'string'              
<series>             ::= <empty> | <stat> <empty> | <stat> ';' <series>
<stat>               ::= <assign> | <if> | <while> | <for>
<assign>             ::= <id> ':=' <exp>
!!!!!!!!!!!

!!!!!!!!!!!
! OPERANDOS                      
<exp>                ::= <primary> | <plus> | <minus> | <conc>
<primary>            ::= <id> | <natural-constant> | <string-constant> | '(' <exp> ')'
<plus>               ::= <exp> '+' <primary>
<minus>              ::= <exp> '-' <primary>
<conc>               ::= <exp> '||' <primary>
!!!!!!!!!!!

!!!!!!!!!!!
! IF E CICLOS                      
<if>                 ::= <if-compact> 'fi' | <if-compact> 'else' <series> 'fi'
<if-compact>         ::= 'if' <exp> 'then' <series>                   

! notação:
!   while _ do
!       (...)
!   od
<while>              ::= 'while' <exp> 'do' <series> 'od'
                      
! notação para o for
!   for _ ; _ ; do
!       (...)
!   od                                                                 
<for-head>           ::= <assign> ';' <exp> ';' <assign>
<for>                ::= 'for' <for-head> 'do' <series> 'od'    
!!!!!!!!!!!

!!!!!!!!!!!                                            
<pico-program>       ::= 'begin' <decls> <series> 'end'                                     
!!!!!!!!!!!
